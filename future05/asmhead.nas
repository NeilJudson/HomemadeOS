; future-os boot asm
; TAB=4

BOTPAK	EQU		0x00280000		; bootpack
DSKCAC	EQU		0x00100000		; 磁盘缓存的地方
DSKCAC0	EQU		0x00008000		; 磁盘缓存的地方（真实模式）

; BOOT_INFO相关信息在内存中的存储地址
CYLS	EQU		0x0ff0			; 存放写入的柱面个数
LEDS	EQU		0x0ff1			; 存放键盘上各种LED指示灯的状态
VMODE	EQU		0x0ff2			; 存放颜色数目的信息，颜色的位数。
SCRNX	EQU		0x0ff4			; 存放分辨率的X
SCRNY	EQU		0x0ff6			; 存放分辨率的Y
VRAM	EQU		0x0ff8			; 存放图像缓冲区的开始地址

		ORG		0xc200			; 指明程序的装载地址

; 设定画面模式

		MOV		AL,0x13			; VGA显卡，320x200x8bit彩色
		MOV		AH,0x00
		INT		0x10
		MOV		BYTE [VMODE],8	; 记录画面模式
		MOV		WORD [SCRNX],320
		MOV		WORD [SCRNY],200
		MOV		DWORD [VRAM],0x000a0000

; 用BIOS取得键盘上各种LED指示灯的状态

		MOV		AH,0x02
		INT		0x16 			; keyboard BIOS
		MOV		[LEDS],AL

; PIC关闭一切中断
;	根据AT兼容机的规格，如果要初始化PIC，
;	必须在CLI之前进行，否则有时会挂起。
;	随后进行PIC的初始化

		MOV		AL,0xff
		OUT		0x21,AL
		NOP						; 如果连续执行OUT指令，有些机种会无法正常运行
		OUT		0xa1,AL

		CLI						; 禁止CPU级别的中断

; CPUから1MB以上のメモリにアクセスできるように、A20GATEをO定

		CALL	waitkbdout
		MOV		AL,0xd1
		OUT		0x64,AL
		CALL	waitkbdout
		MOV		AL,0xdf			; enable A20
		OUT		0x60,AL
		CALL	waitkbdout

; プロテクトモ`ド移行

[INSTRSET "i486p"]				; 486の命令まで使いたいという述

		LGDT	[GDTR0]			; 憾GDTをO定
		MOV		EAX,CR0
		AND		EAX,0x7fffffff	; bit31を0にする（ペ`ジング禁止のため）
		OR		EAX,0x00000001	; bit0を1にする（プロテクトモ`ド移行のため）
		MOV		CR0,EAX
		JMP		pipelineflush
pipelineflush:
		MOV		AX,1*8			;  iみき可能セグメント32bit
		MOV		DS,AX
		MOV		ES,AX
		MOV		FS,AX
		MOV		GS,AX
		MOV		SS,AX

; bootpackの送

		MOV		ESI,bootpack	; 送元
		MOV		EDI,BOTPAK		; 送先
		MOV		ECX,512*1024/4
		CALL	memcpy

; ついでにディスクデ`タも本来の位置へ送

; まずはブ`トセクタから

		MOV		ESI,0x7c00		; 送元
		MOV		EDI,DSKCAC		; 送先
		MOV		ECX,512/4
		CALL	memcpy

; 残り全部

		MOV		ESI,DSKCAC0+512	; 送元
		MOV		EDI,DSKCAC+512	; 送先
		MOV		ECX,0
		MOV		CL,BYTE [CYLS]
		IMUL	ECX,512*18*2/4	; シリンダ数からバイト数/4にQ
		SUB		ECX,512/4		; IPLの分だけ差し引く
		CALL	memcpy

; asmheadでしなければいけないことは全部しKわったので、
;	あとはbootpackに任せる

; bootpackの起

		MOV		EBX,BOTPAK
		MOV		ECX,[EBX+16]
		ADD		ECX,3			; ECX += 3;
		SHR		ECX,2			; ECX /= 4;
		JZ		skip			; 送するべきものがない
		MOV		ESI,[EBX+20]	; 送元
		ADD		ESI,EBX
		MOV		EDI,[EBX+12]	; 送先
		CALL	memcpy
skip:
		MOV		ESP,[EBX+12]	; スタック初期
		JMP		DWORD 2*8:0x0000001b

waitkbdout:
		IN		 AL,0x64
		AND		 AL,0x02
		JNZ		waitkbdout		; ANDのY果が0でなければwaitkbdoutへ
		RET

memcpy:
		MOV		EAX,[ESI]
		ADD		ESI,4
		MOV		[EDI],EAX
		ADD		EDI,4
		SUB		ECX,1
		JNZ		memcpy			; 引き算したY果が0でなければmemcpyへ
		RET
; memcpyはアドレスサイズプリフィクスを入れ忘れなければ、ストリング命令でもける

		ALIGNB	16
GDT0:
		RESB	8				; ヌルセレクタ
		DW		0xffff,0x0000,0x9200,0x00cf	; iみき可能セグメント32bit
		DW		0xffff,0x0000,0x9a28,0x0047	; g行可能セグメント32bit（bootpack用）

		DW		0
GDTR0:
		DW		8*3-1
		DD		GDT0

		ALIGNB	16
bootpack:
